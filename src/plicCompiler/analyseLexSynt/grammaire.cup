package plicCompiler.analyseLexSynt ;

import java.util.ArrayList;

import plicCompiler.arbreAbstrait.ArbreAbstrait;
import plicCompiler.arbreAbstrait.Classe;
import plicCompiler.arbreAbstrait.instruction.Commentaire;
import plicCompiler.arbreAbstrait.instruction.Declaration;
import plicCompiler.arbreAbstrait.instruction.EcrireConstante;
import plicCompiler.arbreAbstrait.instruction.EcrireIdentificateur;
import plicCompiler.arbreAbstrait.instruction.Instruction;
import plicCompiler.arbreAbstrait.instruction.ListeInstruction;
import plicCompiler.arbreAbstrait.instruction.Affectation;
import plicCompiler.arbreAbstrait.instruction.Ecrire;
import plicCompiler.arbreAbstrait.instruction.Lire;
import plicCompiler.arbreAbstrait.expression.Expression;
import plicCompiler.arbreAbstrait.expression.Identificateur;
import plicCompiler.arbreAbstrait.expression.constante.ConstanteArithmetique;
import plicCompiler.arbreAbstrait.expression.constante.ConstanteChaine;
import plicCompiler.arbreAbstrait.expression.constante.ConstanteLogique;
import plicCompiler.arbreAbstrait.expression.operation.operationUnaire.ExpressionNegative;
import plicCompiler.arbreAbstrait.expression.operation.operationBinaire.arithmetique.Add;
import plicCompiler.arbreAbstrait.expression.operation.operationBinaire.arithmetique.Mul;
import plicCompiler.arbreAbstrait.expression.operation.operationBinaire.arithmetique.Sub;
import plicCompiler.arbreAbstrait.expression.operation.operationBinaire.arithmetique.Div;
import plicCompiler.arbreAbstrait.expression.operation.operationBinaire.comparaison.Equal;
import plicCompiler.arbreAbstrait.expression.operation.operationBinaire.comparaison.GreaterThan;
import plicCompiler.arbreAbstrait.expression.operation.operationBinaire.comparaison.LowerThan;
import plicCompiler.arbreAbstrait.expression.operation.operationBinaire.comparaison.Different;
import plicCompiler.exception.SyntaxicException;
import plicCompiler.tDS.Symbole;
import plicCompiler.tDS.TDS;


action code {: /* vide pour le moment */
	    :} ;
   
/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:
    /* Change the method report_error so it will display the line and column of where the error
       occurred in the input as well as the reason for the error which is passed into the method
       in the String 'message'. */
    public void report_error(String message, Object info) {
   
        /* Create a StringBuffer called 'm' with the string 'Error' in it. */
        StringBuilder m = new StringBuilder();
   
        /* Check if the information passed to the method is the same type as the type 
           java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the information in the object 
               info that is being typecasted as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
   
            /* Check if the line number in the input is greater or equal to zero. */
            if (s.left >= 0) {                
                /* Add to the end of the StringBuffer error message the line number of the error 
                   in the input. */
                m.append(s.left);   
                /* Check if the column number in the input is greater or equal to zero. */
                if (s.right >= 0){                    
                    /* Add to the end of the StringBuffer error message the column number of the 
                       error in the input. */
                    //m.append(", column "+(s.right+1));
                }
            }
        }
        /* Add to the end of the StringBuffer error message created in this method the message that
           was passed into this method. */
        m.append(" : "+message);
   
        /* Print the contents of the StringBuffer 'm', which contains an error message, out on 
           a line. */
        
    }
    /* Change the method report_fatal_error so when it reports a fatal error it will display the 
       line and column number of where the fatal error occurred in the input as well as the reason
       for the fatal error which is passed into the method in the object 'message' and then exit.*/
    public void report_fatal_error(String message, Object info) throws SyntaxicException {
    	String ligne = "";
        java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
        if (info instanceof java_cup.runtime.Symbol) {
            if (s.left >= 0) {                
                ligne = String.valueOf(s.left);
            }
        }
        System.out.println("colon : " + s.right);
        throw new SyntaxicException("Symbole inattendu", ligne);
    }
:};
/* ------------Declaration of Terminals and Non Terminals Section----------- */
/* Terminals (tokens returned by the scanner).  
   Terminals that have no value are listed first and then terminals that do have a value, in this 
   case a String value, are listed on the next line down. */

terminal Symbol		POINTVIRGULE;

terminal Symbol		PAROUV;
terminal Symbol		PARFER;

terminal Symbol		DIFF;
terminal Symbol		SUP;
terminal Symbol		INF;
terminal Symbol		EQUAL;

terminal Symbol		PLUS, UPLUS;
terminal Symbol		MINUS, UMINUS;
terminal Symbol		MUL;
terminal Symbol		DIV;

terminal String 	CSTE;
terminal Symbol 	VRAI;
terminal Symbol		FAUX;

terminal Symbol 	VIRGULE;

terminal Symbol		AFFECTIDF;

terminal Symbol END;
terminal Symbol PUBLIC;
terminal Symbol PRIVE;
terminal Symbol ENTIER;
terminal Symbol ECRIREIDF;
terminal Symbol LIREIDF;
terminal Symbol CLASSEIDF;
terminal Symbol COMMENTAIREIDF;

terminal String CHAINE;
terminal String CHAINEGUILL;
terminal String IDF;

   
/* Non terminals used in the grammar section.  
   Non terminals that have an object value are listed first and then non terminals that have an 
   integer value are listed.  An object value means that it can be any type, it isn't set to a 
   specific type.  So it could be an Integer or a String or whatever. */


non terminal ArbreAbstrait SYSTEME;

non terminal Classe CLASSE, CLASSECLASSE;
non terminal ListeInstruction LISTDECL;
non terminal Instruction DECLARATION ;

non terminal Declaration DECL_CHAMP;
non terminal ArrayList<Identificateur> LISTIDF;
non terminal Symbole.Statue STATUT;
non terminal Symbole.Type TYPE;

non terminal Instruction INSTRUCTION;
non terminal Affectation AFFECT;
non terminal Lire LIRE;
non terminal Ecrire ECRIRE;

non terminal Expression EXPR;

non terminal Commentaire COMM;

precedence left PLUS, MINUS, DIFF, SUP, INF;
precedence left MUL, DIV;
precedence left EQUAL;
precedence left UMINUS, UPLUS;

/* ----------------------------Grammar Section-------------------- */

start with SYSTEME;

SYSTEME		::=		CLASSE:c
					{:
						RESULT = c;
					:}
			;

CLASSE		::=		CLASSECLASSE IDF:idf LISTDECL:lst END
					{:
						TDS.getInstance().sortirBlock();
						TDS.getInstance().ajouter(new Identificateur(idf), new Symbole(Symbole.Type.CLASS));
						RESULT = new Classe(new Identificateur(idf), lst);
					:}
			;
			
CLASSECLASSE::=		CLASSEIDF
					{:
						TDS.getInstance().entrerBlock();
					:}
			;

LISTDECL	::=		LISTDECL:lst DECLARATION:d
					{:
						lst.add(d);
						RESULT = lst;
					:}
			|		DECLARATION:d
					{:
						ListeInstruction lst = new ListeInstruction();
						lst.add(d);
						RESULT = lst;
					:}
			;

DECLARATION	::=		DECL_CHAMP:d POINTVIRGULE
					{:
						RESULT = d;
					:}
			|		INSTRUCTION:lst POINTVIRGULE
					{:
						RESULT = lst;
					:}
			;

DECL_CHAMP	::=		STATUT:s TYPE:t LISTIDF:idfs 
					{:
						//System.out.println("fhdqqslfhdskqhfjdk");
						TDS.getInstance().ajouter(idfs, new Symbole(s, t));
						RESULT = new Declaration(idfs, idfsleft+1);
					:}
			;
			
LISTIDF		::=		LISTIDF:list VIRGULE IDF:idf 
					{:
						//System.out.println("coucou");
						list.add(new Identificateur(idf));
						RESULT = list;
					:}
			|		IDF: idf
					{:
						ArrayList<Identificateur> list = new ArrayList<Identificateur>();
						list.add(new Identificateur(idf));
						RESULT = list;
					:}
			;

STATUT		::=		PUBLIC
					{:
						RESULT = Symbole.Statue.PUBLIC;
					:}
			|		PRIVE
					{:
						RESULT = Symbole.Statue.PRIVE;
					:}
			;

TYPE		::=		ENTIER
					{:
						RESULT = Symbole.Type.ENTIER;
					:}
			;

INSTRUCTION	::=		AFFECT:aff
					{:
						RESULT = aff;
					:}
			|		LIRE:l
					{:
						RESULT = l;
					:}
			|		ECRIRE:e
					{:
						RESULT = e;
					:}
			;

AFFECT		::=		IDF:idf AFFECTIDF EXPR:e 
					{:
						RESULT = new Affectation(new Identificateur(idf), e, idfleft+1);
					:}
			;

LIRE		::=		LIREIDF IDF:idf 
					{:
						RESULT = new Lire(new Identificateur(idf), idfleft+1);
					:}
			;

ECRIRE		::=		ECRIREIDF IDF:idf 
					{:
						RESULT = new EcrireIdentificateur(new Identificateur(idf), idfleft+1);
					:}
			|		ECRIREIDF CHAINEGUILL:c 
					{:
						RESULT = new EcrireConstante(new ConstanteChaine(c), cleft+1);
					:}
			;
			
        
EXPR	::=		CSTE:c 
			    {:
					RESULT = new ConstanteArithmetique(c);
		        :}
		|		VRAI
			    {:
					RESULT = new ConstanteLogique("1");
		        :}
		|		FAUX
			    {:
					RESULT = new ConstanteLogique("0");
		        :}
		|		IDF:idf
				{:
					//System.out.printf("je suis un identificateur!!");
					RESULT = new Identificateur(idf);
				:}
		|		EXPR:e1 PLUS EXPR:e2
		        {:
					RESULT = new Add(e1,e2);
		        :}
		|		EXPR:e1 MINUS EXPR:e2
		        {:
					RESULT = new Sub(e1,e2);
		        :}
		|		EXPR:e1 MUL EXPR:e2
		        {:
					RESULT = new Mul(e1,e2);
		        :}
		|		EXPR:e1 DIV EXPR:e2
		        {:
					RESULT = new Div(e1,e2);
		        :}
		|		EXPR:e1 DIFF EXPR:e2
		        {:
					RESULT = new Different(e1,e2);
		        :}
		|		EXPR:e1 SUP EXPR:e2
		        {:
					RESULT = new GreaterThan(e1,e2);
		        :}
		|		EXPR:e1 INF EXPR:e2
		        {:
					RESULT = new LowerThan(e1,e2);
		        :}
		|		EXPR:e1 EQUAL EXPR:e2
		        {:
					RESULT = new Equal(e1,e2);
		        :}
		|		PAROUV EXPR:e PARFER
		        {:
		         	RESULT = e;
		        :}
		|		MINUS EXPR:e
				{:
					RESULT = new ExpressionNegative(e);
				:}  %prec UMINUS
		|		PLUS EXPR:e
				{:
					RESULT = new Expression(e);
				:}  %prec UPLUS
         ;